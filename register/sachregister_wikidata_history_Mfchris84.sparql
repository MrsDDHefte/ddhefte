SELECT ?item ?itemLabel (concat(?issue,"/",?pages) as ?zaehlung) ?schlagwort ?schlagwortLabel ?l ?Epoche WHERE {
  ?item wdt:P1433 wd:Q14916674;
    p:P921 ?mainSubStmt.
  ?mainSubStmt ps:P921 ?schlagwort.
  ?schlagwort rdfs:label ?l filter (lang(?l) = "en").
  ?item wdt:P433 ?issue.
  ?item wdt:P304 ?pages.

  OPTIONAL {
    ?mainSubStmt prov:wasDerivedFrom ?refNode.
    ?refNode pr:P1932 ?DDH_EpocheCode;
      pr:P248 wd:Q14916674.
    BIND(
      IF(STRSTARTS(?DDH_EpocheCode, "D "), "1764 bis 1831",
      IF(STRSTARTS(?DDH_EpocheCode, "E "), "1832 bis 1871", "")) 
     AS ?Epoche)
  }
  FILTER( CONTAINS( ?l, 'history' )).
  SERVICE wikibase:label { bd:serviceParam wikibase:language "de". }
}
